// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = "1.3.72"
        navigation_version = "2.3.0"

        kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        material = 'com.google.android.material:material:1.1.0'

        androidx = [
                coreKtx         : 'androidx.core:core-ktx:1.3.0',
                appcompat       : 'androidx.appcompat:appcompat:1.1.0',
                lifecycleExt    : 'androidx.lifecycle:lifecycle-extensions:2.2.0',
                constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
                navigation      : [
                        fragment: "androidx.navigation:navigation-fragment-ktx:$navigation_version",
                        ui      : "androidx.navigation:navigation-ui-ktx:$navigation_version"
                ]
        ]

        // key-value storage framework
        mmkv = 'com.tencent:mmkv-static:1.2.1'

        // Dependency Injection
        hilt_android_version = "2.28-alpha"
        hilt_version = "1.0.0-alpha01"
        dagger = [
                hiltAndroid : "com.google.dagger:hilt-android:$hilt_android_version",
                hiltAndroidCompiler: "com.google.dagger:hilt-android-compiler:$hilt_android_version",
                hiltCompiler: "androidx.hilt:hilt-compiler:$hilt_version",
                hiltViewModel: "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_version"
        ]

        coroutines_version = "1.3.3"
        coroutines = [
                core   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version",
                android: "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
        ]

        retrofit2_version = "2.7.0"
        retrofit2 = [
                retrofit         : "com.squareup.retrofit2:retrofit: $retrofit2_version",
                coverterMoshi    : "com.squareup.retrofit2:converter-moshi:$retrofit2_version",
                coroutinesAdapter: 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
        ]

        okhttp_version = "4.2.2"
        okhttp3 = [
                okhttp           : "com.squareup.okhttp3:okhttp:$okhttp_version",
                logginInterceptor: "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
        ]

        moshi_version = "1.8.0"
        moshi = [
                moshi : "com.squareup.moshi:moshi:$moshi_version",
                kotlin: "com.squareup.moshi:moshi-kotlin:$moshi_version"
        ]

        ext.getProperty = { filename, propertyName ->
            def propsFile = rootProject.file(filename)
            if (propsFile.exists()) {
                def properties = new Properties()
                properties.load(new FileInputStream(propsFile))
                if (properties[propertyName] != null) {
                    return properties[propertyName]
                } else {
                    print("No such property " + propertyName + " in file " + filename)
                }
            } else {
                print(filename + " does not exist!")
            }
        }
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_android_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}